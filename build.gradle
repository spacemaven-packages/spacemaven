import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
    id 'application'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.21'
    id 'io.ktor.plugin' version '3.2.3'
    id 'gg.jte.gradle' version '3.2.1'
    id 'com.google.cloud.tools.jib' version '3.4.5'
}

application {
    mainClass = 'net.derfruhling.spacemaven.ApplicationKt'
    applicationDefaultJvmArgs = [
            "--enable-native-access=ALL-UNNAMED",
            "-Xms512M",
            "-Xmx512M",
            "-Dotel.java.global-autoconfigure.enabled=true",
            "-Dotel.instrumentation.common.db-statement-sanitizer.enabled=true",
            "-Dotel.service.name=spacemaven",
            "-Dotel.resource.attributes=gcp.project_id=spacemaven",
            "-Dotel.metrics.exporter=otlp",
            "-Dotel.traces.exporter=otlp",
            "-Dotel.logs.exporter=none",
            "-Dotel.exporter.otlp.endpoint=https://telemetry.googleapis.com",
            "-Dotel.exporter.otlp.protocol=http/protobuf",
            "-Dgoogle.cloud.project=spacemaven",
    ]
}

jib {
    container {
        jvmFlags = application.applicationDefaultJvmArgs.toList()
        ports = ["8080/tcp"]
    }
}

repositories {
    mavenCentral()
}

configurations {
    development {

    }

    if(!project.hasProperty("spm.buildingFromDockerImage")) {
        implementation.extendsFrom(development)
    } else {
        compileOnly.extendsFrom(development)
    }
}

dependencies {
    implementation platform('com.google.cloud:libraries-bom:26.61.0')
    implementation platform('org.apache.logging.log4j:log4j-bom:2.25.1')
    implementation platform('io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.18.1')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.21'
    implementation 'io.ktor:ktor-server-core'
    implementation 'io.ktor:ktor-server-auth'
    implementation 'io.ktor:ktor-server-cio'
    implementation 'io.ktor:ktor-server-content-negotiation'
    implementation 'io.ktor:ktor-serialization-kotlinx-json'
    implementation 'io.ktor:ktor-serialization-kotlinx-cbor'
    implementation 'io.ktor:ktor-serialization-kotlinx-xml'
    implementation 'io.ktor:ktor-server-jte'
    implementation 'io.github.oshai:kotlin-logging:7.0.3'

    implementation 'com.google.cloud:google-cloud-datastore:2.31.0'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-ktor-3.0:2.18.1-alpha'

    implementation 'ch.qos.logback:logback-core:1.5.18'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:1.10.2'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.1'

    implementation 'io.opentelemetry:opentelemetry-sdk'
    implementation 'io.opentelemetry:opentelemetry-sdk-extension-autoconfigure'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:2.18.1'
    runtimeOnly "io.opentelemetry.contrib:opentelemetry-gcp-auth-extension:1.48.0-alpha"
    runtimeOnly 'io.opentelemetry.contrib:opentelemetry-gcp-resources:1.48.0-alpha'

    development 'gg.jte:jte-kotlin:3.2.1'

    testImplementation 'io.ktor:ktor-server-test-host'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5:2.1.21'
}

jte {
    generate()

    sourceDirectory = file('src/main/resources/templates').toPath()
}

java {
    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

kotlin {
    target {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_21
        }
    }
}

kotlin.target.compilations.main.getCompileTaskProvider().configure {
    dependsOn('generateJte')
}

kotlin.sourceSets.named("main") {
    kotlin.srcDir project.layout.buildDirectory.get().file('generated-sources/jte')
}

test {
    useJUnitPlatform()
}

tasks.register('dockerBuild', Exec) {
    dependsOn 'installDist'

    def imageName = project.getProperties().get('docker.image', 'spacemaven')
    def tag = project.getProperties().get('docker.tag', 'indev')

    commandLine 'docker', 'build', "--tag=$imageName:$tag", project.rootDir.getAbsolutePath()
}

tasks.register('dockerPush', Exec) {
    dependsOn 'dockerBuild'

    def imageName = project.getProperties().get('docker.image', 'spacemaven')
    def tag = project.getProperties().get('docker.tag', 'indev')

    commandLine 'docker', 'push', "$imageName:$tag"
}
